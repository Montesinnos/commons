plugins {
    id 'maven'
    id 'maven-publish'
    id 'java'
    id 'idea'
    id 'org.unbroken-dome.test-sets' version '1.5.1'
    id 'signing'
    id 'java-library'
}

group 'com.montesinnos.friendly'
version '0.1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            // more goes in here
        }
    }
    repositories {
        mavenLocal()
    }
}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-api
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'

    // https://mvnrepository.com/artifact/com.google.guava/guava
    compile group: 'com.google.guava', name: 'guava', version: '26.0-jre'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.7'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.7'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-csv
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: '2.9.7'


    //
    // TEST
    //
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'
    // https://mvnrepository.com/artifact/org.junit.platform/junit-platform-launcher
    testCompile group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.3.1'
    // https://bintray.com/sta-szek/maven/pojo-tester/0.7.6
    testCompile 'pl.pojo:pojo-tester:0.7.6'
    // https://mvnrepository.com/artifact/org.assertj/assertj-core
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'

}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

testSets {
    integrationTest
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

//Uses this to push data up to Maven
task cleanBuildPublish(type: GradleBuild) {
    tasks = ['clean', 'build' , 'uploadArchives']
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Friendly Commons for Lazy Developers'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Do you want to get stuff done without putting effort?'
                url 'https://github.com/Montesinnos/commons'

                developers {
                    developer {
                        id 'montesinnos'
                        name 'Montesinnos'
                        email 'email@montesinnos.com'
                    }
                }
            }
        }
    }
}